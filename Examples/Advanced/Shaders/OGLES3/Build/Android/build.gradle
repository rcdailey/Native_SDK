import org.apache.tools.ant.taskdefs.condition.Os
	
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

File pvrSdkDir = file("${project.buildDir}/../../../../../../../")

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.powervr.OGLES3Shaders"
        minSdkVersion 18
        targetSdkVersion 19
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jni.srcDirs= []
            jniLibs.srcDir 'libs'

        }
    }

    FileCollection tree = files(
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/Basetex.pvr",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/Reflection.pvr",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/Cubemap.pvr",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/AnisoMap.pvr",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/anisotropic_lighting.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/directional_lighting.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/envmap.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/fasttnl.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/lattice.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/phong_lighting.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/point_lighting.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/reflections.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/simple.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/spot_lighting.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/toon.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/vertex_sine.pfx",
"${pvrSdkDir}/Examples/Advanced/Shaders/OGLES3/wood.pfx",

)
    task copyAssets(type: Copy){
        from tree
        into 'assets'
    }

    String ndkBuildCommand = 'ndk-build';

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndkBuildCommand = 'ndk-build.cmd'
    }
    task ndkBuildTools(type:Exec) {
        workingDir "${pvrSdkDir}/Tools/OGLES3/Build/Android"
        commandLine ndkBuildCommand,  '-j'
    }

    task ndkBuild(type: Exec, dependsOn:[copyAssets ,ndkBuildTools]) {
        commandLine ndkBuildCommand, '-j'
    }

    task ndkBuildClean(type: Exec) {
        commandLine ndkBuildCommand, '-j', 'clean'
    }

    task copyToBin(type: Copy) {
        from 'build/outputs/apk/'
        into 'bin/'
        include('*.apk')
   }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    assembleRelease {
        doLast() {
            copyToBin.execute()
        }
    }

    clean.dependsOn ndkBuildClean
    clean {
        doLast() {
            delete "assets"
            delete "obj"
            delete "bin"
        }
    }

    preBuild.dependsOn copyAssets
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
